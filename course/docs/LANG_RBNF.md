# Базовые грамматические правила языка

<буква> :: =    a |
                b |
                c |
                d |
                e |
                f |
                g |
                h |
                i |
                j |
                k |
                l |
                m |
                n |
                o |
                p |
                q |
                r |
                s |
                t |
                u |
                v |
                w |
                x |
                y |
                z |
                A |
                B |
                C |
                D |
                E |
                F |
                G |
                H |
                I |
                J |
                K |
                L |
                M |
                N |
                O |
                P |
                Q |
                R |
                S |
                T |
                U |
                V |
                W |
                X |
                Y |
                Z

<цифра> ::= 0 |
            1 |
            2 |
            3 |
            4 |
            5 |
            6 |
            7 |
            8 |
            9

<идентификатор> ::= <буква> { <буква> | <цифра> }

<число>::=  <целое> |
            <действительное>

<целое>::=  <двоичное> |
            <восьмеричное> |
            <десятичное> |
            <шестнадцатеричное>

<двоичное>::= {/ 0 | 1 /} (B | b)

<восьмеричное>::= {/ 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 /} (O | o)

<десятичное>::= {/ <цифра> /} [D | d]

<шестнадцатеричное>::= <цифра> {<цифра> | A | B | C | D | E | F | a | b | c | d | e | f} (H | h)

<действительное>::= <числовая_строка> <порядок> | [<числовая_строка>] . <числовая_строка> [порядок]

<числовая_строка>::= {/ <цифра> /}

<порядок>::= ( E | e )[+ | -] <числовая_строка>

<выражение> ::= <сумма> |
                <выражение> <операции_группы_отношения> <сумма>

<сумма> ::= <произведение> { <операции_группы_сложения> <произведение>}

<произведение> ::= <множитель> { <операции_группы_умножения> <множитель>}

<множитель> ::= <идентификатор> |
                <число> |
                <логическая_константа> |
                <унарная_операция> <множитель> |
                «(» <выражение> «)

<логическая_константа>:: = true | false

# Специфика модельного языка

1. Операции языка

<операции_группы_отношения>::=  != |
                                = = |
                                < |
                                <= |
                                > |
                                >=

<операции_группы_сложения>::=   + |
                                - |
                                ||

<операции_группы_умножения>::=  * |
                                / |
                                &&

<унарная_операция>::= !



2. Правила, определяющие структуру программы
<программа>::= «{» {/ (<описание> | <оператор>) ; /} «}»



3. Правила, определяющие раздел описания переменных
<описание>::= <тип> <идентификатор> { , <идентификатор> }



4. Правила, определяющие типы данных
<тип>::= int | float | bool



5. Правило, определяющее оператор программы

<оператор>::=   <составной> |
                <присваивания> |
                <условный> |
                <фиксированного_цикла> |
                <условного_цикла> |
                <ввода> |
                <вывода>

<составной>::= begin <оператор> { ; <оператор> } end

<присваивания>::= <идентификатор> := <выражение>

<условный>::= if «(»<выражение> «)» <оператор> [else <оператор>]

<фиксированного_цикла>::= for <присваивания> to <выражение> [step <выражение>] <оператор> next

<условного_цикла>::= while «(»<выражение> «)» <оператор>

<ввода>::= readln идентификатор {, <идентификатор> }

<вывода>::= writeln <выражение> {, <выражение> }


############################################################
Метаязык, предложенный Бэкусом и Науром, использует следующие обозначения:
- символ «::=» отделяет левую часть правила от правой (читается: «определяется как»);
- нетерминалы обозначаются произвольной символьной строкой, заключенной в угловые скобки «<» и «>»;
- терминалы - это символы, используемые в описываемом языке;
- правило может определять порождение нескольких альтернативных цепочек, отделяемых друг от друга символом вертикальной черты «|» (читается: «или»).

Для повышения удобства и компактности описаний в РБНФ вводятся следующие дополнительные конструкции (метасимволы):
- квадратные скобки «[» и «]» означают, что заключенная в них синтаксическая конструкция может отсутствовать;
- фигурные скобки «{» и «}» означают повторение заключенной в них синтаксической конструкции ноль или более раз;
- сочетание фигурных скобок и косой черты «{/» и «/}» используется для
обозначения повторения один и более раз;
- круглые скобки «(» и «)» используются для ограничения альтернативных
конструкций.