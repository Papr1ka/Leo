ПРОГРАММА → '{' ОПИСАНИЕ_ОПЕРАТОР_ПОСЛ '}'

ОПИСАНИЕ_ОПЕРАТОР_ПОСЛ →    ОПИСАНИЕ_ОПЕРАТОР |
                            ОПИСАНИЕ_ОПЕРАТОР ОПИСАНИЕ_ОПЕРАТОР_ПОСЛ

ОПИСАНИЕ_ОПЕРАТОР → ОПИСАНИЕ | ОПЕРАТОР

ОПИСАНИЕ →  ТИП ИДЕНТИФИКАТОР_ПОСЛ

ИДЕНТИФИКАТОР_ПОСЛ →    ИДЕНТИФИКАТОР |
                        ИДЕНТИФИКАТОР ',' ИДЕНТИФИКАТОР_ПОСЛ

ТИП →   int | float | bool

ОПЕРАТОР →  СОСТАВНОЙ |
            ПРИСВАИВАНИЯ |
            УСЛОВНЫЙ |
            ФИКСИРОВАННОГО_ЦИКЛА |
            УСЛОВНОГО_ЦИКЛА |
            ВВОДА |
            ВЫВОДА

СОСТАВНОЙ → begin ОПЕРАТОР_ПОСЛ end

ОПЕРАТОР_ПОСЛ → ОПЕРАТОР |
                ОПЕРАТОР ';' ОПЕРАТОР_ПОСЛ

ПРИСВАИВАНИЯ →  ИДЕНТИФИКАТОР ':=' ВЫРАЖЕНИЕ

УСЛОВНЫЙ →  if '(' ВЫРАЖЕНИЕ ')' ОПЕРАТОР |
            if '(' ВЫРАЖЕНИЕ ')' ОПЕРАТОР else ОПЕРАТОР

ФИКСИРОВАННОГО_ЦИКЛА →  for ПРИСВАИВАНИЯ to ВЫРАЖЕНИЕ ОПЕРАТОР next |
                        for ПРИСВАИВАНИЯ to ВЫРАЖЕНИЕ step ВЫРАЖЕНИЕ ОПЕРАТОР next

УСЛОВНОГО_ЦИКЛА →   while '(' ВЫРАЖЕНИЕ ')' ОПЕРАТОР

ВВОДА → readln ИДЕНТИФИКАТОР_ПОСЛ

ВЫРАЖЕНИЕ_ПОСЛ →    ВЫРАЖЕНИЕ |
                    ВЫРАЖЕНИЕ ',' ВЫРАЖЕНИЕ_ПОСЛ

ВЫВОДА →    writeln ВЫРАЖЕНИЕ_ПОСЛ

ВЫРАЖЕНИЕ → СУММА |
            СУММА ОПЕРАЦИИ_ОТНОШЕНИЯ ВЫРАЖЕНИЕ

СУММА → ПРОИЗВЕДЕНИЕ |
        ПРОИЗВЕДЕНИЕ ОПЕРАЦИИ_СЛОЖЕНИЯ СУММА

ПРОИЗВЕДЕНИЕ →  МНОЖИТЕЛЬ |
                МНОЖИТЕЛЬ ОПЕРАЦИИ_УМНОЖЕНИЯ ПРОИЗВЕДЕНИЕ

МНОЖИТЕЛЬ → ИДЕНТИФИКАТОР |
            ЧИСЛО |
            ЛОГИЧЕСКАЯ_КОНСТАНТА |
            УНАРНАЯ_ОПЕРАЦИЯ МНОЖИТЕЛЬ |
            '(' ВЫРАЖЕНИЕ ')'

ЛОГИЧЕСКАЯ_КОНСТАНТА →  true |
                        false

ОПЕРАЦИИ_ОТНОШЕНИЯ →    '!=' |
                        '==' |
                        '<' |
                        '<=' |
                        '>' |
                        '>='

ОПЕРАЦИИ_СЛОЖЕНИЯ → '+' |
                    '-' |
                    '||'

ОПЕРАЦИИ_УМНОЖЕНИЯ →    '*' |
                        '/' |
                        '&&'

УНАРНАЯ_ОПЕРАЦИЯ →  '!'

ИДЕНТИФИКАТОР → БУКВА |
                БУКВА БУКВА_ЦИФРА_ПОСЛ

БУКВА_ЦИФРА_ПОСЛ →  БУКВА_ЦИФРА |
                    БУКВА_ЦИФРА БУКВА_ЦИФРА_ПОСЛ

БУКВА_ЦИФРА →   БУКВА |
                ЦИФРА

БУКВА → a |
        b |
        ...
        z |
        A |
        B |
        ...
        Z

ЦИФРА → 0 |
        1 |
        ...
        9

ЧИСЛО → ЦЕЛОЕ |
        ДЕЙСТВИТЕЛЬНОЕ

ЦЕЛОЕ → ДВОИЧНОЕ |
        ВОСЬМИРИЧНОЕ |
        ДЕСЯТИЧНОЕ |
        ШЕСТНАДЦАТЕРИЧНОЕ

ДВОИЧНОЕ →  НОЛЬ_ОДИН_ПОСЛ 'B' |
            НОЛЬ_ОДИН_ПОСЛ 'b'

НОЛЬ_ОДИН_ПОСЛ →    НОЛЬ_ОДИН |
                    НОЛЬ_ОДИН НОЛЬ_ОДИН_ПОСЛ

НОЛЬ_ОДИН → '0' |
            '1'

ВОСЬМИРИЧНОЕ →  НОЛЬ_СЕМЬ_ПОСЛ 'O' |
                НОЛЬ_СЕМЬ_ПОСЛ 'o'

НОЛЬ_СЕМЬ_ПОСЛ →    НОЛЬ_СЕМЬ |
                    НОЛЬ_СЕМЬ НОЛЬ_СЕМЬ_ПОСЛ

НОЛЬ_СЕМЬ → 0 |
            1 |
            ...
            7

ДЕСЯТИЧНОЕ →    ЦИФРА_ПОСЛ |
                ЦИФРА_ПОСЛ 'D' |
                ЦИФРА_ПОСЛ 'd'

ЦИФРА_ПОСЛ →    ЦИФРА |
                ЦИФРА ЦИФРА_ПОСЛ

ШЕСТНАДЦАТЕРИЧНОЕ → ЦИФРА 'H' |
                    ЦИФРА 'h' |
                    ЦИФРА НОЛЬ_ПЯТНАДЦАТЬ_ПОСЛ 'H' |
                    ЦИФРА НОЛЬ_ПЯТНАДЦАТЬ_ПОСЛ 'h'

НОЛЬ_ПЯТНАДЦАТЬ_ПОСЛ →  НОЛЬ_ПЯТНАДЦАТЬ |
                        НОЛЬ_ПЯТНАДЦАТЬ НОЛЬ_ПЯТНАДЦАТЬ_ПОСЛ

НОЛЬ_ПЯТНАДЦАТЬ →   ЦИФРА |
                    'a' |
                    'b' |
                    ...
                    'f' |
                    'A' |
                    'B' |
                    ...
                    'F'

ДЕЙСТВИТЕЛЬНОЕ →    ЦИФРА_ПОСЛ ПОРЯДОК |
                    '.' ЦИФРА_ПОСЛ |
                    ЦИФРА_ПОСЛ '.' ЦИФРА_ПОСЛ |
                    '.' ЦИФРА_ПОСЛ ПОРЯДОК |
                    ЦИФРА_ПОСЛ '.' ЦИФРА_ПОСЛ ПОРЯДОК

ПОРЯДОК →   БУКВА_Е ЦИФРА_ПОСЛ |
            БУКВА_Е '+' ЦИФРА_ПОСЛ |
            БУКВА_Е '-' ЦИФРА_ПОСЛ

БУКВА_Е →   'E' |
            'e'
